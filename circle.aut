               JOB  Draw a circle
               CTL  3311

     *** Index variables.
               ORG  87
     X1        DSA  0

     *** Main routine.
               ORG  333
     START     MCW  ONE,Y          Reset Y

     *** Next line.
     NXTLN     MCW  ONE,X1         Reset X
               CS   299            Clear printer

     * Center Y
               MCW  Y,CENY
               S    @050@,CENY
     * Square centered Y.
               ZA   CENY,YSQR-4
               A    CENY,YSQR-4
               A    CENY,YSQR-4
               M    CENY,YSQR

     *** Next pixel.
     NXTPX     NOP

     * Center X
               MCW  X1,CENX
               S    @050@,CENX
     * Square centered X.
               ZA   CENX,XSQR-4
               M    CENX,XSQR

     * Add to YSQR
               ZA   XSQR,DSQR
               A    YSQR,DSQR

     * See if we're inside the circle.
               C    DSQR,RSQR
               BL   SKDRAW

     * Draw pixel.
               MN   STAR,200&X1
               MZ   STAR,200&X1

     SKDRAW    NOP

     *** Increment X.
               A    ONE,X1

     *** See if we're done.
               C    X1,WIDTH
               BL   DONELN
               B    NXTPX

     *** Print our line.
     DONELN    W

     *** Increment Y.
               A    ONE,Y

     *** See if we're done.
               C    HEIGHT,Y
               BH   FINIS
               B    NXTLN

     *** Done.
     FINIS     H    FINIS
               H

     *** Variables.
     Y         DCW  000
     PRNLOC    DCW  000

     *** Constants.
     WIDTH     DCW  100
     HEIGHT    DCW  100
     ONE       DCW  001
     STAR      DC   @*@
     RSQR      DCW  0001600

     *** Work area.
     CENX      DCW  000         Centered X
     CENY      DCW  000         Centered Y
     XSQR      DCW  0000000     Squared centered X
     YSQR      DCW  0000000     Squared centered Y
     DSQR      DCW  0000000     Distance from center


               END  START
